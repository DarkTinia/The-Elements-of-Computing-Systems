// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    
    Not(in = instruction[15], out = isA);
    Not(in = isA, out = isC);

    //C-code or A-Code A Register
    And(a = isC, b = instruction[5], out = loadA);
    Mux16(a = instruction, b = outALU, sel = loadA, out = outA);

    Or(a = isA, b = loadA, out = loadA2);
    ARegister(in = outA, load = loadA2, out = Areg, out[0..14] = addressM);

    Mux16(a = Areg, b = inM, sel = instruction[12], out = AM);

    //C-code M
    And(a = isC, b = instruction[3], out = writeM);

    //C-code D Register
    And(a = isC, b = instruction[4], out = loadD);
    DRegister(in = outALU, load = loadD, out = Dreg);


    //comp
    And(a = isC, b = instruction[6], out = no);
    And(a = isC, b = instruction[7], out = f);
    And(a = isC, b = instruction[8], out = ny);
    And(a = isC, b = instruction[9], out = zy);
    And(a = isC, b = instruction[10], out = nx);
    And(a = isC, b = instruction[11], out = zx);

    ALU(x = Dreg, y = AM, zx = zx, nx = nx, zy = zy, ny = ny, f = f, no = no, out = outALU, out = outM, zr = zr, ng = ng);
    
    
    //jump
    And(a = instruction[0], b = isC, out = isJGT);
    And(a = instruction[1], b = isC, out = isJEQ);
    And(a = instruction[2], b = isC, out = isJLT);
    
    And(a = isJEQ, b = zr, out = JEQ);
    And(a = isJLT, b = ng, out = JLT);
    Or(a = zr, b = ng, out = tr);
    Not(in = tr, out = tr1);
    And(a = isJGT, b = tr1, out = JGT);
    Or(a = JEQ, b = JLT, out = Jmp1);
    Or(a = Jmp1, b = JGT, out = Jmp2);
    PC(in = Areg, load = Jmp2, inc = true, reset = reset, out[0..14] = pc);  
}